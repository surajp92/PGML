reset
set term x11 enhanced font 'Helvetica, 18'

U0 = 0.2

eta(r, s) = r * sqrt(U0 / (2 * 1e-6 * s))

set key inside bottom right vertical Left
set pointsize 1.5

set xr [0:4]
set yr [0:1.2]
set xlabel '{/Symbol h} [-]'
set ylabel 'U_x [m/s]'

plot 'BlasiusF.dat' u ($1):( $3) t 'Blasius solution' w l,\
     'x030_Ux_T.xy' every 2 u ( eta($1, 0.03) ):($2/U0)  \
     		    t 'x = 3.0cm' w points pt 5,\
     'x025_Ux_T.xy' every 2 u ( eta($1, 0.025) ):($2/U0)  \
     		    t 'x = 2.5cm' w points pt 6,\
     'x015_Ux_T.xy' every 2 u ( eta($1, 0.015) ):($2/U0) \
     		    t 'x = 1.5cm' w points pt 9,\
     'x005_Ux_T.xy' every 2 u ( eta($1, 0.005) ):($2/U0) \
     		    t 'x = 0.5cm' w points pt 11
